buildscript {
    ext.kotlin_version = '1.9.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    afterEvaluate {
        if (it.hasProperty('android')) {
            def javaVersion = JavaVersion.VERSION_11
            android {
                def androidApiVersion = 33
                compileSdk androidApiVersion

                if(namespace == null){
                    def manifest = new XmlSlurper().parse(file(sourceSets.main.manifest.srcFile))
                    def packageName = manifest.@package.text()
                    println("[${project.name}] set android.namespace to ${packageName}")
                    namespace = packageName
                }

                defaultConfig {
                    targetSdkVersion androidApiVersion
                }

                compileOptions {
                    sourceCompatibility javaVersion
                    targetCompatibility javaVersion
                }

                buildFeatures {
                    buildConfig true
                }

                tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                    kotlinOptions {
                        jvmTarget = javaVersion.toString()
                    }
                }
            }
        } else {
            // println("[${project.name}] is not an android project")
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
